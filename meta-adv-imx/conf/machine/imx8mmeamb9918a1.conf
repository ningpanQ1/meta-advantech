#@TYPE: Machine
#@NAME: Advantech i.MX8MM LPDDR4 EAMB9918 A1
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for Advantech i.MX8MM LPDDR4 EAMB9918 Board
#@MAINTAINER: jwzl <cnj1990@sina.com.cn>

MACHINEOVERRIDES =. "adv-bsp:imx-boot-container:mx8:mx8m:mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES += "pci wifi bluetooth"

# OP-TEE is also applicable to NXP BSP, mainline BSP seems not to have
# a full support for it yet.
MACHINE_FEATURES:append:use-nxp-bsp = " optee"

# Set kernel DTB
KERNEL_DEVICETREE = " \
	freescale/imx8mm-evk.dtb \
	freescale/imx8mm-adv-eamb9918-a1.dtb \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mm-adv-eamb9918-a1.dtb"

# uboot binary.
IMX_DEFAULT_BOOTLOADER = "u-boot-imx"
UBOOT_SUFFIX = "bin"
SPL_BINARY = "spl/u-boot-spl.bin"
LOADADDR = ""
UBOOT_MAKE_TARGET = "all"
IMX_BOOT_SEEK = "33"

# uboot config
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]       = "imx8mm_adv_eamb9918_2g_defconfig,sdcard"
UBOOT_CONFIG[mfgtool]  = "imx8mm_adv_eamb9918_2g_defconfig"
UBOOT_CONFIG[fspi]   = "imx8mm_adv_eamb9918_2g_defconfig"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
	lpddr4_pmu_train_1d_imem.bin \
	lpddr4_pmu_train_1d_dmem.bin \
	lpddr4_pmu_train_2d_imem.bin \
	lpddr4_pmu_train_2d_dmem.bin \
"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_evk_flexspi', 'flash_evk', d)}"
IMX_BOOT_SOC_TARGET = "iMX8MM"

ATF_PLATFORM = "imx8mm"
ATF_LOAD_ADDR = "0x920000"
OPTEE_BIN_EXT = "8mm"
TEE_LOAD_ADDR = "0xbe000000"

# Extra firmware package name, that is required to build boot container for fslc bsp
IMX_EXTRA_FIRMWARE = "firmware-imx-8m"

# serial debug port settting
SERIAL_CONSOLES = "115200;ttymxc1"

# advantech default WKS file for sdcard/emmc parttion image.
WKS_FILE = "adv-imx-image-bootpart.wks.in"
WKS_FILE_DEPENDS:append = " imx-m4-demos"
IMAGE_BOOT_FILES:append = " \
    imx8mm_m4_TCM_hello_world.bin \
    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mm_m4_TCM_sai_low_power_audio.bin \
"

IMAGE_RELEASE_VERSION = "eamb9918-sdcard_2.0.0"

## Override the hostname.
hostname:pn-base-files = "eamb9918"

# append WIFI and Bluetooth module
IMAGE_INSTALL:append = "rs9116nb0"
